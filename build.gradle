plugins {
    id 'org.springframework.boot' version '2.2.6.RELEASE'
    id 'io.spring.dependency-management' version '1.0.9.RELEASE'
    id 'java'
    id 'com.google.cloud.tools.jib' version '2.2.0'
}

group = projectGroup
version = projectVersion
sourceCompatibility = javaVersion
targetCompatibility = javaVersion

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenLocal()
    maven { url 'https://maven.aliyun.com/repository/central/' }
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-web'
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'
    annotationProcessor 'org.projectlombok:lombok'
    testImplementation('org.springframework.boot:spring-boot-starter-test') {
        exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
    }
}

test {
    useJUnitPlatform()
}

springBoot {
    buildInfo()
}

jib {
    from {
        image = 'nas.joylau.cn:5007/public/openjdk:8'
    }
    allowInsecureRegistries = true
    to {
        image = 'joyfay/joy-gallery-spider'
        credHelper = 'osxkeychain'
        tags = ['' + version]
    }
    container {
        creationTime = 'USE_CURRENT_TIMESTAMP'
        appRoot = '/home/liufa/app/joy-gallery-spider'
        jvmFlags = [
                '-Xms512m',
                '-Djava.security.egd=file:/dev/./urandom',
                '-Duser.timezone=GMT+08',
        ]
        mainClass = 'cn.joylau.code.JoyGallerySpiderApplication'
        workingDirectory = '/home/liufa/app/joy-gallery-spider'
        volumes = ['/home/liufa/app/joy-gallery-spider/gallery']
        ports = ['8080']
        environment = [
                logLevel: 'info',
                args: ''
        ]
        args = [
                '--save.path=/home/liufa/app/joy-gallery-spider/gallery',
                '--logging.level.org.springframework.web.client.RestTemplate=${logLevel}',
                '--logging.level.cn.joylau.code=${logLevel}',
                '${args}'
        ]
        labels = [Descripttion: 'Gallery Spider Project For Spring Boot']
    }
}